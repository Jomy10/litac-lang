import "mem"
import "phase_result"
import "array"
import "map"
import "module"
import "string_view"
import "system"
import "symbols"
import "preprocessor"

public const MAX_PREFIX_SIZE = 32

public struct Lita {
    allocator: *const Allocator // TODO proper arena allocator
    preprocessor: Preprocessor
    result: PhaseResult
    modules: Map<*const char, *Module>
    metrics: [MetricType.MAX_METRIC_TYPES]Metric

    totalSourceLines: u32
    totalAllocations: u32

    options: *LitaOptions

    programSymbols: ProgramSymbols
}

@asStr
public enum MetricType {
    PARSING,
    TYPE_CHECKING,
    CGEN,
    CWRITE,
    CCOMPILE,

    MAX_METRIC_TYPES
}

public struct Metric {
    bytesAllocated: usize
    allocationCount: u32
    executionTime: f64

    startTime: f64
}

public struct LitaOptions {
    srcPath: [MAX_PATH]char        // the directory of where the main source module is
    libPath: [MAX_PATH]char        // optional configurable path to modules for libraries
    litaPath: [MAX_PATH]char       // the LITAC_HOME environment variable for standard lita modules

    inputFile: [MAX_PATH]char      // the input source file
    outputFile: [MAX_PATH]char     // the C output file
    outputPath: [MAX_PATH]char     // the C output directory

    compileCmd: *const char        // the C compile system command

    checkerOnly: bool              // only do the checker
    cOutputOnly: bool              // generates C file only, does not compile it
    run: bool                      // runs after successful compilation
    showProfileInfo: bool          // outputs profiling information

    cPrefix: [MAX_PREFIX_SIZE]char // Prefix for types/variables defined in C
}

var linearAllocator = LinearAllocator{}

public func (this: *Lita) init(options: *LitaOptions) {
    var size: usize = 1024 * 1024 * 500;
    linearAllocator.init(defaultAllocator.alloc(size), size)

    var allocator = (&linearAllocator) as (*const Allocator)

    this.allocator = allocator
    this.options = options
    this.totalSourceLines = 0
    this.totalAllocations = 0
    this.preprocessor.init(this)
    this.result = PhaseResultInit(allocator)
    this.modules = StrMap<*Module>(null, 32, allocator)
}

public func (lita: *Lita) free() {
    var allocator = lita.allocator as (*LinearAllocator)
    defaultAllocator.free(allocator.mem)
    /*
    for(var i = 0; i < allocator.allocations.size(); i += 1) {
        var a = allocator.allocations.get(i)
        allocator.decorated.free(a.addr)
        allocator.decorated.free(a)
    }
    debugAllocator.free()
    */
}

public func (lita: *Lita) addModule(module: *Module) {
    lita.modules.put(module.id.filename, module)
}

public func (lita: *Lita) startCapture(type: MetricType) {
    var metric = &lita.metrics[type]

    var allocator = lita.allocator as (*LinearAllocator)
    metric.bytesAllocated = allocator.totalBytesAllocated
    metric.allocationCount = allocator.totalAllocations
    metric.startTime = SystemTimeMSec()
}

public func (lita: *Lita) endCapture(type: MetricType) {
    var metric = &lita.metrics[type]

    var allocator = lita.allocator as (*LinearAllocator)
    metric.bytesAllocated = allocator.totalBytesAllocated - metric.bytesAllocated
    metric.allocationCount = allocator.totalAllocations - metric.allocationCount
    metric.executionTime = SystemTimeMSec() - metric.startTime
}