import "lita"
import "mem"
import "string_buffer"
import "string"
import "json"
import "libc"
import "map"
import "system"
import "phase_result"
import "array"
import "lsp/workspace"
import "lsp/protocol"

const IN_BUFFER_SIZE = 1024 * 1024 * 2
const VERSION = "0.12" // TODO: read from a file at compile time; share with lita.main

public struct LspServer {
    requestAllocator: LinearAllocator
    applicationAllocator: LinearAllocator

    lita: *Lita
    message: StringBuffer
    output: StringBuffer
    outbound: StringBuffer

    isInitialized: bool
    isRunning: bool

    workspace: Workspace

    logFile: *FILE
}

public func (this: *LspServer) init(lita: *Lita) {
    this.lita = lita
    this.isRunning = false
    this.isInitialized = false

    this.logFile = fopen("lsp1.log", "w")
    if(!this.logFile) {
        printf("Couldn't open log file!\n")
    }

    var size: usize = 1024 * 1024 * 100;
    this.requestAllocator.init(defaultAllocator.alloc(size), size)
    this.applicationAllocator.init(defaultAllocator.alloc(size), size)

    this.message = StringBufferInit(IN_BUFFER_SIZE, defaultAllocator)
    this.output = StringBufferInit(IN_BUFFER_SIZE, defaultAllocator)
    this.outbound = StringBufferInit(IN_BUFFER_SIZE, defaultAllocator)

    this.workspace.init(this, this.appAlloc())
}


public func (this: *LspServer) requestAlloc() : *const Allocator {
    return (&this.requestAllocator) as (*const Allocator)
}


public func (this: *LspServer) appAlloc() : *const Allocator {
    return (&this.applicationAllocator) as (*const Allocator)
}

public func (this: *LspServer) free() {
    if(this.applicationAllocator.mem) {
        defaultAllocator.free(this.applicationAllocator.mem)
    }
    if(this.requestAllocator.mem) {
        defaultAllocator.free(this.requestAllocator.mem)
    }
    this.message.free()
    this.output.free()

    if(this.logFile) {
        fclose(this.logFile)
    }
}


public func (this: *LspServer) start() {
    this.log("starting Lita Language Server v%s\n", VERSION)
    defer this.log("terminating Lita Language Server\n")

    this.isRunning = true
    while(this.isRunning) {
        this.readMessage()
    }
}

public func (this: *LspServer) log(format: *const char, ...) {
    var args: va_list;
    va_start(args, format);
    vfprintf(this.logFile, format, args);
    va_end(args);
    fflush(this.logFile)
}

public func (this: *LspServer) readMessage() {
    this.message.clear()

    this.log("Waiting for message..\n")
    var size = this.readHeader()
    if(size < 0) {
        this.writeErrorResponse(0, "error reading 'Content-Length'")
        return;
    }

    // free memory
    defer this.requestAllocator.currentOffset = 0

    this.log("Received message size: %d\n", size)
    this.message.reserve(size)

    // Read content-size
    var bytesRead = fread(this.message.buffer, 1, size, stdin)
    this.log("Received %d bytes from fread\n", bytesRead)

    if(bytesRead != size) {
        this.log("message received size of %d did not match Content-Length: %d\n", bytesRead, size)
        size = bytesRead as (i32)
        //this.log("   message: '%.*s'\n", bytesRead, this.message.buffer)
        //this.writeErrorResponse(0, "message received size did not match content-length")
        //return;
    }

    this.message.length = size
    this.log("Received message: '%.*s'\n", size, this.message.buffer)

    var parser = JsonParserInit(this.requestAlloc())
    defer parser.free()

    var json = parser.parseJson(this.message.cStr())
    if(parser.hasError()) {
        this.log("json parsing error: %s\n", parser.errorMsg)
        return;
    }

    this.handleMessage(json)
}

func (this: *LspServer) readHeader() : i32 {
    var header = [256]char{0};
    var sizeIndex = 0
    var index = 0

    this.log("Reading header:\n'")
    while(true) {
        var r = getchar()
        if(r == EOF) {
            break
        }

        var c = r as (char)
        this.log("%c", c)

        if(c == ' ') {
            sizeIndex = index + 1
        }

        if(c == '\n' || c == '\r') {
            header[index] = '\0'
            break
        }
        else {
            header[index] = c
        }

        index += 1
    }

    this.log("'\nRead header: '%s'\n", header)

    if(strncmp(header, "Content-Length: ", sizeIndex) != 0) {
        this.log("Invalid header: '%s'\n", header)
        return -1
    }

    var size = atoi(&header[sizeIndex])
    return size + 1 /* for the \r\n\r\n */
}

public func (this: *LspServer) handleMessage(msg: *JsonNode) {
    var method = msg.getStr("method", null)
    if(!method) {
        this.writeErrorResponse(0, "no method defined")
        return;
    }

    var params = msg.getObject("params")
    if(strcmp(method, "initialize") == 0) {
        this.handleInitializeMessage(msg, params)
    }
    else if(strcmp(method, "initialized") == 0) {
        this.isInitialized = true
    }
    else if(!this.isInitialized) {
        this.writeErrorResponse(0, "not yet properly initialized")
    }
    else if(strcmp(method, "textDocument/didOpen") == 0) {
        this.handleTextDocumentOpenMessage(msg, params)
    }
    else if(strcmp(method, "textDocument/documentSymbol") == 0) {
        this.handleTextDocumentSymbolMessage(msg, params)
    }
    else if(strcmp(method, "textDocument/didSave") == 0) {
        this.handleTextDocumentSaveMessage(msg, params)
    }
    else if(strcmp(method, "textDocument/references") == 0) {
        this.handleTextDocumentReferencesMessage(msg, params)
    }
    else if(strcmp(method, "$/cancelRequest") == 0) {
        // TODO: do nothing for right now
    }
    else {
        this.writeErrorResponse(0, "unsupported method: '%s'", method)
    }
}

public func (this: *LspServer) handleInitializeMessage(msg: *JsonNode, params: *JsonNode) {
    var rootPath = params.getStr("rootPath", "")
    var rootUri = params.getStr("rootUri", "")
    this.log("Root Path = '%s'\n", rootPath)

    var path = [MAX_PATH]char{0}
    UriToFilePath(rootUri, path)

    this.log("Root Uri = '%s' as path: '%s'\n", rootUri, path)

    this.workspace.setup(path, rootUri)

    var capabilities = """
    {
        "capabilities": {
            "textDocumentSync": 2,
            "definitionProvider": true,
            "documentSymbolProvider": true,
            "workspaceSymbolProvider": true,
            "referencesProvider": true,
            "completionProvider": {
                "resolveProvider": true,
                "triggerCharacters": [
                    "."
                ]
            }
        }
    }"""

    var resp = this.createResponse(msg.getInt("id"))

    var parser = JsonParserInit(this.requestAlloc())
    resp.put("result", parser.parseJson(capabilities))
    resp.put("error", JSON_NULL)

    this.writeResponse(resp)
}


public func (this: *LspServer) handleTextDocumentOpenMessage(msg: *JsonNode, params: *JsonNode) {
    var textDoc = params.getObject("textDocument")
    if(!textDoc) {
        return;
    }

    var uri = textDoc.getStr("uri", null)
    this.workspace.openDocument(uri, textDoc.getStr("text", null))
    this.writeDiagnostic(uri)
}

public func (this: *LspServer) handleTextDocumentSymbolMessage(msg: *JsonNode, params: *JsonNode) {
    var textDoc = params.getObject("textDocument")
    if(!textDoc) {
        return;
    }

    var uri = textDoc.getStr("uri", null)
    var results = this.workspace.documentSymbols(uri, this.requestAlloc())

    var resp = this.createResponse(msg.getInt("id"))
    resp.put("result", results)
    resp.put("error", JSON_NULL)

    this.writeResponse(resp)
}

public func (this: *LspServer) handleTextDocumentSaveMessage(msg: *JsonNode, params: *JsonNode) {
    var textDoc = params.getObject("textDocument")
    if(!textDoc) {
        return;
    }

    var uri = textDoc.getStr("uri", null)
    this.workspace.saveDocument(uri)
    this.writeDiagnostic(uri)
}

public func (this: *LspServer) handleTextDocumentReferencesMessage(msg: *JsonNode, params: *JsonNode) {
    var textDoc = params.getObject("textDocument")
    if(!textDoc) {
        return;
    }

    var uri = textDoc.getStr("uri", null)
    var pos = params.getObject("position")
    var results = this.workspace.references(uri, pos, this.requestAlloc())

    var resp = this.createResponse(msg.getInt("id"))
    resp.put("result", results)
    resp.put("error", JSON_NULL)

    this.writeResponse(resp)
}


public func (this: *LspServer) writeDiagnostic(docUri: *const char) {
    this.log("Running diagnostics for: '%s'\n", docUri)
    var result = this.workspace.runDiagnostics(this.requestAlloc())

    var params = CreateJsonObject(this.requestAlloc())
    params.putStr("uri", docUri)
    var diagnostics = CreateJsonArray(this.requestAlloc())
    params.put("diagnostics", diagnostics)

    var resp = CreateJsonObject(this.requestAlloc())
    resp.putStr("method", "textDocument/publishDiagnostics")
    resp.put("params", params)

    var output = [MAX_PATH]char{0}
    var openedDocFilename = UriToFilePath(docUri, output)

    if(!result.errors.empty()) {
        var outputPath = [MAX_PATH]char{0}
        for(var i = 0; i < result.errors.size(); i += 1) {
            var error =  result.errors.get(i)

            // TODO: when does this happen?
            if(!error.pos.filename) {
                continue
            }

            var modulePathOfError = GetAbsolutePath(this.workspace.rootPath, error.pos.filename, outputPath)
            //this.log("Comparing path: '%s' vs '%s'\n", path, filename)

            // TODO: should we be only sending over errors in this file?
            if(strcmp(modulePathOfError, openedDocFilename) != 0) {
                continue
            }

            var diagnostic = CreateJsonObject(this.requestAlloc())
            diagnostic.putStr("message", error.message)
            diagnostic.putIntNumber("severity", 1)
            var sourceLine = StringCopy(error.pos.start, (error.pos.end - error.pos.start) as (i32), this.requestAlloc())
            diagnostic.putStr("source", sourceLine)
            diagnostic.put("range", SrcPosToRange(error.pos, this.requestAlloc()))

            diagnostics.add(diagnostic)
        }
    }

    this.writeResponse(resp)
}

public func (this: *LspServer) writeErrorResponse(id: i64, format: *const char, ...) {
    var args: va_list;
    va_start(args, format);
    var sb = StringBufferInit(512, this.requestAlloc())
    sb.appendArgs(format, args)
    va_end(args);


    var error = CreateJsonObject(this.requestAlloc())
    error.putStr("message", sb.cStr())

    var resp = this.createResponse(id)
    resp.put("error", error)

    this.writeResponse(resp)
}

public func (this: *LspServer) writeResponse(msg: *JsonNode) {
    this.output.clear()
    this.outbound.clear()

    //var header = [256]char{0};
    //var headerStr = StringInit(header, 256)

    var jsonStr = msg.print(this.output)
    var length = this.output.length

    if(length < 1) {
        return;
    }


    //headerStr.append("Content-Length: %d\r\n\r\n", length)
    this.outbound.append("Content-Length: %d\n\n%.*s", length, length, this.output.buffer)
    this.log("Sending: Len: %d %.*s\n", this.outbound.length, this.outbound.length, this.outbound.buffer)

    //fwrite(header as (*const void), sizeof(:char), headerStr.length, stdout)
    fwrite(this.outbound.cStr() as (*const void), sizeof(:char), this.outbound.length, stdout)
    //printf("%.*s", this.outbound.length, this.outbound.buffer)
    fflush(stdout)
}

public func (this: *LspServer) createResponse(id: i64) : *JsonNode {
    var response = CreateJsonObject(this.requestAlloc())
    response.putStr("jsonrpc", "2.0")

    if(id > -1) {
        response.putIntNumber("id", id)
    }

    return response
}
